plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.diffplug.spotless" version "6.23.0"
    id "net.ltgt.errorprone" version "4.4.0"
}

group = 'com'
version = '0.0.1-SNAPSHOT'
description = 'init'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    /* ------------------------ INIT ------------------------ */
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.0'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // JWT
    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.12.5'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.12.5'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.12.5'

    // Web Client
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // Cache - Caffeine
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine'

    // Querydsl
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // JSpecify + NullAway
    implementation 'org.jspecify:jspecify:1.0.0'
    annotationProcessor 'org.jspecify:jspecify:1.0.0'
    errorprone 'com.uber.nullaway:nullaway:0.12.15'
    errorprone 'com.google.errorprone:error_prone_core:2.46.0'

    /* ------------------------ TEST ------------------------ */
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

spotless {
    java {
        removeUnusedImports() // 사용하지 않는 import 제거
        trimTrailingWhitespace() // 라인 끝의 불필요한 공백 제거
        endWithNewline() // 파일의 마지막에 개행 추가
    }
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        disableAllChecks = false
        option("NullAway:OnlyNullMarked", "true")
        error("NullAway")
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

clean {
    delete file('src/main/generated')
}
